// Global settings
WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

multi_line_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
single_line_comment = @{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

COMMENT = _{ multi_line_comment | single_line_comment }

// Positive integers
int = @{ "0" | ASCII_NONZERO_DIGIT ~ (ASCII_DIGIT)* }

// Lower letters are the only variables, and they are global.
id = { ASCII_ALPHA_LOWER }

// Expressions are always terms, as in C: "a = 5" has the value "5"
term = { paren_expr | id | int }

// Everything is a number here, therefore terms are already sums
summation = { term ~ "+" ~ sum }
substraction = { term ~ "-" ~ sum }
sum = { summation | substraction | term }

// True if "sum" is different from 0, or if $1 < $2
comparison = { sum ~ "<" ~ sum }

// An expression
assignment = { id ~ "=" ~ expr }
expr = !{ assignment | comparison | sum }

// An expression surrounded by parenthesis
paren_expr = { "(" ~ expr ~ ")"}

////////////////////////
//       Prints       //
////////////////////////
string = @{ (!(NEWLINE | "\"") ~ ANY)* }

printable = ${ ("\"" ~ string ~ "\"") | expr }

print_statement = { "print" ~ "(" ~ printable ~ ")" ~ ";" }

////////////////////////
//     Functions      //
////////////////////////
function_name = @{ ASCII_ALPHA ~ ASCII_ALPHANUMERIC* }

// The parameter list of functions is always a list of ids
id_parameter_list = { id ~ (", " ~ id)* }

// The parameter list when calling functions can be made up of expressions.
// Assignment of results only gets done if the parameter list contains ids.
free_parameter_list = { expr ~ (", " ~ expr)* }

function_declaration_statement = { "fn" ~ function_name ~ "(" ~ id_parameter_list ~ ")" ~ statement }

function_usage_statement = { "call" ~ function_name ~ "(" ~ free_parameter_list ~ ")" ~ ";" }

// All the different Statements
if_else = { "if" ~ paren_expr ~ statement ~ "else" ~ statement }
_if = { "if" ~ paren_expr ~ statement }
do_while = { "do" ~ statement ~ "while" ~ paren_expr ~ ";" }
_while = { "while" ~ paren_expr ~ statement }
scoped_statement = { "{" ~ statement+ ~ "}" }
expr_statement = { expr ~ ";" }
semicolon_statement = { ";" }

//
statement = { 
	if_else 
	| _if 
	| do_while 
	| _while 
	| scoped_statement 
	| expr_statement
	| semicolon_statement
	| print_statement
  | function_declaration_statement
  | function_usage_statement
} 

// The language
tiny_c = _{ SOI ~ statement ~ EOI }
